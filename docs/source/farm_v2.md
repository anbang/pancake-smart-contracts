# Pancake Farm V2 Ê∫êÁ†ÅËØ¶Ëß£

Pancake Smart Contracts V2 ÁâàÊú¨ Farm Ê∫êÁ†ÅÁöÑËØ¶ÁªÜËß£ËØª

Âú® bscscan ‰∏äËÆ§ËØÅÁöÑÂêçÂ≠óÊòØ **PancakeSwap: Main Staking Contract**„ÄÇ

- BscScan Âú∞ÂùÄ: [0xa5f8C5Dbd5F286960b9d90548680aE5ebFf07652](https://bscscan.com/address/0xa5f8C5Dbd5F286960b9d90548680aE5ebFf07652#code)
- Github Âú∞ÂùÄ:
  - Ê≤°ÊúâÂºÄÊîæ Github ÁöÑ‰ª£Á†ÅÔºå‰ΩÜÊòØÊµèËßàÂô®ÂÜÖÂèØ‰ª•ÁúãÂà∞

## ÂâçÁΩÆ‰ªãÁªç

ËøòÊ≤°ÊúâËØ¶ÁªÜÊï¥ÁêÜÔºåÊöÇÊó∂Ê¢≥ÁêÜ‰∫ÜÊ°ÜÊû∂

## V2 ÂØπÊØî V1 ÁöÑÊîπÂèò

### V2 ‰∏≠ÁöÑÊîπÂèò

- ËÆ°ÁÆóÊ±†Â≠ê‰∏≠Âå∫Âùó‰πãÈó¥ÁöÑÂ•ñÂä±Êó∂ÔºåÁ≤æÂ∫¶Áî±`1e12` Êîπ‰∏∫ `1e18`ÔºåËÆ°ÁÆóÂ•ñÂä±Êõ¥Á≤æÁ°Æ

## Áä∂ÊÄÅÂèòÈáè

MasterChef ‰∏≠ÂåÖÂê´‰∏§‰∏™‰∏ªË¶ÅÁöÑÊï∞ÊçÆÁªìÊûÑ: `UserInfo` Âíå `PoolInfo`

### UserInfo : userInfo

```
// ÊØè‰∏™Áî®Êà∑ÈÉΩÊúâËøô‰∏™Êï∞ÊçÆ‰ø°ÊÅØ
struct UserInfo {
    uint256 amount;             // Áî®Êà∑Ë¥®ÊäºÁöÑLPTokenÊï∞Èáè
    uint256 rewardDebt;         // Áî®Êà∑Â∑≤ÁªèÊî∂Âà∞ÁöÑÂ•ñÂä±Êï∞Èáè
    uint256 boostMultiplier;    // Âä†ÊùÉ‰πòÊï∞(V2Êñ∞Â¢û)
}

// ÊØè‰∏™ÊåÅÊúâ LP ‰ª£Â∏ÅÁöÑÁî®Êà∑ÁöÑ‰ø°ÊÅØ
// mapping ‰∏≠ÁöÑ uint256 ÊòØÊ±†Â≠êÁöÑ pid
// mapping ‰∏≠ÁöÑ address ÊòØÁî®Êà∑ÁöÑ address
mapping (uint256 => mapping(address => UserInfo)) public userInfo;
```

`rewardDebt` ËøôÈáåÂÅö‰∏Ä‰∫õÂ§çÊùÇÁöÑÊï∞Â≠¶ËøêÁÆó„ÄÇ‰øùËØÅ‰ªª‰ΩïÊó∂Èó¥ÁÇπÔºåÁ≠âÂæÖÂàÜÂèëÁªôÁî®Êà∑ CAKE ÁöÑÊï∞ÈáèÊòØÔºö`ÂæÖÂèëÂ•ñÂä± = (user.amount * pool.accCakePerShare) - user.rewardDebt`

ÊØèÂΩìÁî®Êà∑ÂØπÊ±†Â≠êËøõË°å LP ‰ª£Â∏ÅÁöÑÂ≠òÂèñÊó∂Ôºå‰ª£Á†ÅÂÜÖÂÅöÂ¶Ç‰∏ãÊìç‰ΩúÔºö

1. Ê±†ÁöÑ `accCakePerShare` Âíå `lastRewardBlock` ËøõË°åÊõ¥Êñ∞„ÄÇ
   1. ËØ•ÂäüËÉΩÂú® `updatePool()` ÂÜÖÂÆûÁé∞ **Êõ¥Êñ∞Ê±†Â≠êÁ≥ªÊï∞**
2. ËÆ°ÁÆóÂæÖÂèëÂ•ñÂä±ÔºåÂπ∂Áõ¥Êé•ËΩ¨ÁªôÁî®Êà∑Âú∞ÂùÄ„ÄÇ
   1. ËØ•ÂäüËÉΩÂú® `settlePendingCake()` ÂÜÖÂÆûÁé∞ **Áî®Êà∑ÂæÖÂèëÂ•ñÂä±ÁªìÁÆó**
3. Êõ¥Êñ∞Áî®Êà∑ÁöÑ `amount`
4. Êõ¥Êñ∞Áî®Êà∑ÁöÑ `rewardDebt`

> ÂèòÂåñ: Ê≠§Â§ÑÊØîËµ∑ V1 ÁâàÊú¨ÔºåÂ¢ûÂä†‰∫Ü `boostMultiplier` ÁöÑÂä†ÊùÉ‰πòÊï∞„ÄÇ

> Ê≥®: accCakePerShare Âú® PoolInfo ÂÜÖËØ¶ÁªÜ‰ªãÁªç„ÄÇ

### PoolInfo : poolInfo

```
// ÊØè‰∏™LPÊ±†ÁöÑ‰ø°ÊÅØ
struct PoolInfo {
    uint256 accCakePerShare;    // Ë¥®Êäº1 wei LPÁöÑÂÖ®Â±ÄCAKEÊî∂ÁõäÔºõËÆ°ÁÆóÊó∂‰ºö‰πò‰ª• 1e18
    uint256 lastRewardBlock;    // ÊúÄÂêé‰∏ÄÊ¨°ÂàÜÈÖç CAKEs ÁöÑÂå∫ÂùóÂè∑
    uint256 allocPoint;         // ËØ•LPÊ±†ÁöÑÂàÜÈÖçÁÇπÊï∞„ÄÇCAKEÊåâÂùóÂàÜÈÖç

    uint256 totalBoostedShare;  // ÊÄªÊèêÂçá‰ªΩÈ¢ù
    bool isRegular;             // ÊòØÂê¶‰∏∫ÊôÆÈÄöÊ±†
}

PoolInfo[] public poolInfo;   // poolInfo ÂÇ®Â≠òÊâÄÊúâLPÊ±†ÁöÑ‰ø°ÊÅØ
```

- allocPoint :ÂàÜÈÖçÁÇπÊï∞ÔºåË¥®ÊäºÊ±†ÁöÑÂàÜÈÖçÊØî‰æãÔºåÂèØ‰ª•ÈÄöËøáË∞ÉÊï¥ÂÆÉÊù•ËÆæÁΩÆ LP ÊåñÁüøÁöÑÊùÉÈáç
- lastRewardBlock :ÊúÄÂêé‰∏ÄÊ¨°ÂàÜÈÖç CAKE ÁöÑÂùóÔºå‰ª£Á†ÅËÆ°ÁÆóÊó∂‰ª£Ë°®"‰∏ä‰∏ÄÊ¨°ÂàÜÈÖçÂ•ñÂä±ÁöÑÂå∫ÂùóÊï∞"
- accCakePerShare :‰ªéÊ±†Â≠êÂàõÂª∫Êó∂Ë¥®Êäº 1 ‰∏™ wei LPToken ,Âà∞ÁõÆÂâç‰∏∫Ê≠¢ÔºõÂú®ÂΩìÂâçÊ±†Â≠êÁöÑ CAKE Êî∂Áõä

**Ê±†Â≠êÂ•ñÂä±ËÆ°ÁÆóÊñπÊ≥ïÂ¶Ç‰∏ã**Ôºà2 ‰∏™Âå∫Âùó‰πãÈó¥ÁöÑÂ•ñÂä±ÔºâÔºö

- accCakePerShare ÈªòËÆ§ÂÄºÊòØ 0ÔºåÊõ¥Êñ∞ÊñπÊ≥ïÂ¶Ç‰∏ã
- 2 ‰∏™Âå∫Âùó‰πãÈó¥ÁöÑÊ±†Â≠êÂ•ñÂä±ÊòØ:
  - `cakeReward = Âçï‰∏™ÂùóÁöÑ CAKE Â•ñÂä± * (pool.allocPoint / totalAllocPoint) * ‰∏§‰∏™Âùó‰πãÈó¥ÁöÑÂùóÊï∞`
  - Âíå V1 Áõ∏ÊØîÔºåËøôÈáå totalAllocPoint Ê†πÊçÆÊ±†Â≠ê‰∏çÂêåÂàÜ‰∏∫ `totalRegularAllocPoint` Âíå `totalSpecialAllocPoint`,ÈÄâÊã©ÂêàÈÄÇÁöÑËÆ°ÁÆó
- Ê±†Â≠êÁöÑÊÄªÈáèÊòØ lpSupply ‰∏™ÔºåÂàô 1 wei LP ÁöÑÂú®‰∏§‰∏™Âå∫ÂùóÈó¥ÁöÑË¥®ÊäºÂ•ñÂä±ÊòØ
  - `(cakeReward / lpSupply)`
- Áî®Êà∑ÁöÑËµÑ‰∫ßÊòØ‰∏ÄÁõ¥ÊîæÂú®Ê±†Â≠êÂÜÖÁöÑÔºåÈô§‰∫ÜÂΩìÂâç‰∏§‰∏™Âå∫Âùó‰πãÈó¥ÁöÑÂ•ñÂä±Ôºå‰πãÂâçÁöÑÂùó‰πüÊòØÊúâÂ•ñÂä±ÁöÑÔºåÊâÄ‰ª•ËøôÈáåÁöÑ pool.accCakePerShare ÈúÄË¶ÅÁ¥ØÂä†„ÄÇ
  - `pool.accCakePerShare = pool.accCakePerShare + (cakeReward * 1e18 / lpSupply)`
  - ËøôÈáåÂºïÂÖ• `1e18` ÊòØ‰∏∫‰∫ÜÁ≤æÂ∫¶ÔºåÊâÄ‰ª•ËÆ°ÁÆóÁî®Êà∑Â•ñÂä±‰ª•ÂèäÁªôÁî®Êà∑ÂèëÂ•ñÂä±ÁöÑÊó∂ÂÄôÔºåÂèØ‰ª•ÈÄöËøáÈô§‰ª• `1e18` ÂæóÂà∞ÁúüÂÆûÂÄºÔºõÊ∫êÁ†ÅÊ≥®ÈáäÈáåËØ¥‰ΩøÁî®ÁöÑÊòØ`1e12`ËÆ°ÁÆóÔºåÂÖ∂ÂÆûÊòØ‰ΩøÁî®Â∏∏Èáè`ACC_CAKE_PRECISION` ÁöÑ `1e18` Á≤æÂ∫¶Êù•ËÆ°ÁÆó„ÄÇ

**Áî®Êà∑Â•ñÂä±ËÆ°ÁÆóÊñπÊ≥ïÂ¶Ç‰∏ã**Ôºà2 ‰∏™Âå∫Âùó‰πãÈó¥ÁöÑÂ•ñÂä±ÔºâÔºö

Áî®Êà∑‰æùËµñ`user.amount`Ôºå`user.rewardDebt` Âíå `pool.accCakePerShare`ËÆ°ÁÆóÂÆûÈôÖÊî∂Áõä„ÄÇÂéüÁêÜÂ¶Ç‰∏ã:

- Áî®Êà∑Âú®Ë¥®Êäº LPToken Êó∂Ôºå`user.amount` ÁªìÂêà `pool.accCakePerShare` ËÆ∞‰∏ãÊù•‰Ωú‰∏∫Ëµ∑ÂßãÁÇπ‰ΩçÔºåË°®Á§∫Â∑≤ÁªèÂèëÁªôÁî®Êà∑ÁöÑÂ•ñÂä±`user.rewardDebt`.
- Áî®Êà∑Âú®Ëß£Êäº LPToken Êó∂ÔºåÁî®`user.amount` ÁªìÂêàÊúÄÊñ∞ `pool.accCakePerShare` ËÆ°ÁÆóÁ¥ØÁßØÂ•ñÂä±„ÄÇ
- Ëøô‰∏ÄÊÆµÊó∂Èó¥ÂÜÖÁöÑÂÆûÈôÖÊî∂ÁõäÂèØ‰ª•ÈÄöËøáÁ¥ØÁßØÂ•ñÂä±ÂáèÂéª‰πãÂâçÁöÑ`user.rewardDebt`ÂæóÂà∞

‰∏äÈù¢Ëøô‰∏™ËÆ°ÁÆóÊî∂ÁõäÁöÑÂéüÁêÜÔºåÂ∞±ÊòØ `settlePendingCake()` Ëøô‰∏™Áî®Êà∑ÂæÖÂèëÂ•ñÂä±ÁªìÁÆóÁöÑÊñπÊ≥ïÊ†∏ÂøÉ„ÄÇ

ÂèòÂåñ: Áõ∏ÊØî V1 ÁâàÊú¨ÔºåÁßªÈô§‰∫Ü`lpToken`,Êñ∞Â¢û‰∫Ü`totalBoostedShare` Âíå `isRegular`„ÄÇ

- `totalBoostedShare` ÊØè‰∏™Ê±†‰∏≠ÁöÑÁî®Êà∑‰ªΩÈ¢ùÊÄªÈáè„ÄÇ
- `isRegular`ÔºöËÆæÁΩÆÂΩìÂâç LP Ê±†ÊòØÂ∏∏ËßÑÊ±†ÔºåËøòÊòØÁâπÊÆäÊ±†
  - **regular pools**: V2 farms
  - **special pools**:"ÁâπÊÆäÊ±†"‰ΩøÁî®‰∏çÂêåÁöÑ"allocPoint"ÈõÜÂêàÂíåÂÆÉ‰ª¨Ëá™Â∑±ÁöÑ"totalSpecialAllocPoint"ÔºåÊó®Âú®Â§ÑÁêÜ CAKE Â•ñÂä±ÂêëÊâÄÊúâ PancakeSwap ‰∫ßÂìÅÁöÑÂàÜÈÖç„ÄÇ

### ÂÖ∂‰ªñÁä∂ÊÄÅÂèòÈáè

```
// Farm V1 ÂêàÁ∫¶ÁöÑÂú∞ÂùÄ: ‰æõ V2 ËøûÊé• V1
IMasterChef public immutable MASTER_CHEF;

// CAKE ÂêàÁ∫¶Âú∞ÂùÄ
IBEP20 public immutable CAKE;

// Áî®‰∫éÊé•Êî∂ÊâÄÊúâ burn CAKE ÁöÑÂîØ‰∏ÄÂú∞ÂùÄ
address public burnAdmin;

// Â§ÑÁêÜ share boosts ÁöÑÂêàÁ∫¶
address public boostContract;

// V2Ê±†ÊâÄÊúâ LP token ÁöÑÂú∞ÂùÄÈõÜÂêà
IBEP20[] public lpToken;

// ÂÖÅËÆ∏Â≠òÊîæÂú®ÁâπÊÆäÊ±†(special pools)‰∏≠ÁöÑÂú∞ÂùÄÁôΩÂêçÂçï„ÄÇ
mapping(address => bool) public whiteList;

// Ê®°ÊãüV1ÁöÑ pool id
uint256 public immutable MASTER_PID;

// ÊâÄÊúâ regular pool ÁöÑÊÄªÂàÜÈÖçÁÇπÊï∞„ÄÇÂøÖÈ°ª‰∏éÊâÄÊúâÂ∏∏ËßÑÊ±†ÁöÑAllocPointÂåπÈÖç
uint256 public totalRegularAllocPoint;

// ÊâÄÊúâ special pool ÁöÑÊÄªÂàÜÈÖçÁÇπÊï∞„ÄÇÂøÖÈ°ª‰∏éÊâÄÊúâÁâπÊÆäÊ±†ÁöÑAllocPointÂåπÈÖç
uint256 public totalSpecialAllocPoint;

// V1 ‰∏≠ÊØèÂùó 40 ‰∏™CAKEÂ∏Å
uint256 public constant MASTERCHEF_CAKE_PER_BLOCK = 40 * 1e18;
uint256 public constant ACC_CAKE_PRECISION = 1e18;

// Âü∫Êú¨ÁöÑÂä†ÊùÉÁ≥ªÊï∞Ôºà‰∏çÂ≠òÂú®ÈíàÂØπÁî®Êà∑ÁöÑÂä†ÊùÉÁ≥ªÊï∞Ôºâ
uint256 public constant BOOST_PRECISION = 100 * 1e10;

// ÊúÄÂ§ßÂä†ÊùÉÁ≥ªÊï∞ÁöÑÂº∫Âà∂ÈôêÂà∂ÔºåËØ•ÂÄºÂøÖÈ°ªÂ§ß‰∫é BOOST_PRECISION
uint256 public constant MAX_BOOST_PRECISION = 200 * 1e10;

// ÊÄª CAKE ‰∫ßÂá∫ÁéáÁöÑÁ≤æÂ∫¶; ÂøÖÈ°ªÁ≠â‰∫é‰∏ãÈù¢‰∏â‰∏™ cakeRate ÁöÑÊÄªÂíå
// total cake rate = toBurn + toRegular + toSpecial
uint256 public constant CAKE_RATE_TOTAL_PRECISION = 1e12;

// Ê≠£Âú®ÊâßË°å CAKE ÈîÄÊØÅÂä®‰ΩúÁöÑÊúÄÂêéÂå∫ÂùóÂè∑„ÄÇ
// CAKE distribution % for burn
uint256 public cakeRateToBurn = 643750000000;

// CAKE distribute % for regular farm pool
uint256 public cakeRateToRegularFarm = 62847222222;

// CAKE distribute % for special pools
uint256 public cakeRateToSpecialFarm = 293402777778;

// ÊúÄÂêé Burned ÁöÑÂùóÂè∑
uint256 public lastBurnedBlock;
```

## ÊûÑÈÄ†ÂáΩÊï∞

‰ª£Á†ÅÂ¶Ç‰∏ã: MasterChefV2 ÈÉ®ÁΩ≤Êó∂ÈúÄË¶ÅÂàùÂßãÂåñÁöÑÂèÇÊï∞

```
constructor(
    IMasterChef _MASTER_CHEF,
    IBEP20 _CAKE,
    uint256 _MASTER_PID,
    address _burnAdmin
) public {
    MASTER_CHEF = _MASTER_CHEF;
    CAKE = _CAKE;
    MASTER_PID = _MASTER_PID;
    burnAdmin = _burnAdmin;
}
```

ÂèÇÊï∞Ëß£ÊûêÂ¶Ç‰∏ã

- MASTER_CHEF: Pancake Master Chef V1 ÁâàÊú¨ÁöÑÂêàÁ∫¶Âú∞ÂùÄ
- CAKE: CAKE Â∏ÅÁöÑÂêàÁ∫¶Âú∞ÂùÄ
- MASTER_PID: V1 ÁâàÊú¨‰∏äÊ®°ÊãüÊ±†ÁöÑ pool id
- burnAdmin: Êé•Êî∂ÈîÄÊØÅ CAKE ÁöÑÂú∞ÂùÄ

Âú® V1 ÁâàÊú¨ÂÜÖÔºåÊúâ Staking Ê±† Âíå ÊôÆÈÄö pool ÁöÑÂå∫ÂàÜÔºåÂπ∂‰∏îÂú®ÊûÑÈÄ†ÂáΩÊï∞ÂÜÖÂàùÂßãÂåñ Staking Ê±†„ÄÇËÄåÂú® V2 ÁâàÊú¨ÔºåÂàÜ‰∏∫ÊôÆÈÄöÊ±†ÂíåÁâπÊÆäÊ±†„ÄÇ

## updatePool Êõ¥Êñ∞Ê±†Â≠êÁ≥ªÊï∞

```
function updatePool(uint256 _pid) public returns (PoolInfo memory pool) {
    pool = poolInfo[_pid];
    if (block.number > pool.lastRewardBlock) {
        uint256 lpSupply = pool.totalBoostedShare;
        uint256 totalAllocPoint = (pool.isRegular ? totalRegularAllocPoint : totalSpecialAllocPoint);

        if (lpSupply > 0 && totalAllocPoint > 0) {
            uint256 multiplier = block.number.sub(pool.lastRewardBlock);
            uint256 cakeReward = multiplier.mul(cakePerBlock(pool.isRegular)).mul(pool.allocPoint).div(
                totalAllocPoint
            );
            pool.accCakePerShare = pool.accCakePerShare.add((cakeReward.mul(ACC_CAKE_PRECISION).div(lpSupply)));
        }
        pool.lastRewardBlock = block.number;
        poolInfo[_pid] = pool;
        emit UpdatePool(_pid, pool.lastRewardBlock, lpSupply, pool.accCakePerShare);
    }
}
```

## settlePendingCake Áî®Êà∑ÂæÖÂèëÂ•ñÂä±ÁªìÁÆó

Â∞ÜÂæÖÂèëÁªôÁî®Êà∑ÁöÑ CAKE Â•ñÂä±ÁªôÁªìÁÆóÊéâ

```
function settlePendingCake(
    address _user,
    uint256 _pid,
    uint256 _boostMultiplier
) internal {
    UserInfo memory user = userInfo[_pid][_user];

    uint256 boostedAmount = user.amount.mul(_boostMultiplier).div(BOOST_PRECISION);
    uint256 accCake = boostedAmount.mul(poolInfo[_pid].accCakePerShare).div(ACC_CAKE_PRECISION);
    uint256 pending = accCake.sub(user.rewardDebt);
    // SafeTransfer CAKE
    _safeTransfer(_user, pending);
}
```

## Ê†∏ÂøÉ

- init:`init(IBEP20 dummyToken)`
  - üîí: onlyOwner
- add:`add(_allocPoint, _lpToken, _isRegular, _withUpdate)`
  - üîí: onlyOwner
- set:`set(_pid, _allocPoint, _withUpdate)`
  - üîí: onlyOwner
- deposit:`deposit(_pid, _amount)`
- withdraw:`withdraw(_pid, _amount)`

### init

```
// Deposits a dummy token to `MASTER_CHEF` MCV1. This is required because MCV1 holds the minting permission of CAKE.
/// It will transfer all the `dummyToken` in the tx sender address.
/// The allocation point for the dummy pool on MCV1 should be equal to the total amount of allocPoint.
/// @param dummyToken The address of the BEP-20 token to be deposited into MCV1.
function init(IBEP20 dummyToken) external onlyOwner {
    uint256 balance = dummyToken.balanceOf(msg.sender);
    require(balance != 0, "MasterChefV2: Balance must exceed 0");
    dummyToken.safeTransferFrom(msg.sender, address(this), balance);
    dummyToken.approve(address(MASTER_CHEF), balance);
    MASTER_CHEF.deposit(MASTER_PID, balance);
    // MCV2 start to earn CAKE reward from current block in MCV1 pool
    lastBurnedBlock = block.number;
    emit Init();
}
```

### add

```
// Add a new pool. Can only be called by the owner.
/// DO NOT add the same LP token more than once. Rewards will be messed up if you do.
/// @param _allocPoint Number of allocation points for the new pool.
/// @param _lpToken Address of the LP BEP-20 token.
/// @param _isRegular Whether the pool is regular or special. LP farms are always "regular". "Special" pools are
/// @param _withUpdate Whether call "massUpdatePools" operation.
/// only for CAKE distributions within PancakeSwap products.
function add(
    uint256 _allocPoint,
    IBEP20 _lpToken,
    bool _isRegular,
    bool _withUpdate
) external onlyOwner {
    require(_lpToken.balanceOf(address(this)) >= 0, "None BEP20 tokens");
    // stake CAKE token will cause staked token and reward token mixed up,
    // may cause staked tokens withdraw as reward token,never do it.
    require(_lpToken != CAKE, "CAKE token can't be added to farm pools");

    if (_withUpdate) {
        massUpdatePools();
    }

    if (_isRegular) {
        totalRegularAllocPoint = totalRegularAllocPoint.add(_allocPoint);
    } else {
        totalSpecialAllocPoint = totalSpecialAllocPoint.add(_allocPoint);
    }
    lpToken.push(_lpToken);

    poolInfo.push(
        PoolInfo({
    allocPoint: _allocPoint,
    lastRewardBlock: block.number,
    accCakePerShare: 0,
    isRegular: _isRegular,
    totalBoostedShare: 0
    })
    );
    emit AddPool(lpToken.length.sub(1), _allocPoint, _lpToken, _isRegular);
}
```

### set

```
// Update the given pool's CAKE allocation point. Can only be called by the owner.
/// @param _pid The id of the pool. See `poolInfo`.
/// @param _allocPoint New number of allocation points for the pool.
/// @param _withUpdate Whether call "massUpdatePools" operation.
function set(
    uint256 _pid,
    uint256 _allocPoint,
    bool _withUpdate
) external onlyOwner {
    // No matter _withUpdate is true or false, we need to execute updatePool once before set the pool parameters.
    updatePool(_pid);

    if (_withUpdate) {
        massUpdatePools();
    }

    if (poolInfo[_pid].isRegular) {
        totalRegularAllocPoint = totalRegularAllocPoint.sub(poolInfo[_pid].allocPoint).add(_allocPoint);
    } else {
        totalSpecialAllocPoint = totalSpecialAllocPoint.sub(poolInfo[_pid].allocPoint).add(_allocPoint);
    }
    poolInfo[_pid].allocPoint = _allocPoint;
    emit SetPool(_pid, _allocPoint);
}
```

### deposit

```
// Deposit LP tokens to pool.
/// @param _pid The id of the pool. See `poolInfo`.
/// @param _amount Amount of LP tokens to deposit.
function deposit(uint256 _pid, uint256 _amount) external nonReentrant {
    PoolInfo memory pool = updatePool(_pid);
    UserInfo storage user = userInfo[_pid][msg.sender];

    require(
        pool.isRegular || whiteList[msg.sender],
        "MasterChefV2: The address is not available to deposit in this pool"
    );

    uint256 multiplier = getBoostMultiplier(msg.sender, _pid);

    if (user.amount > 0) {
        settlePendingCake(msg.sender, _pid, multiplier);
    }

    if (_amount > 0) {
        uint256 before = lpToken[_pid].balanceOf(address(this));
        lpToken[_pid].safeTransferFrom(msg.sender, address(this), _amount);
        _amount = lpToken[_pid].balanceOf(address(this)).sub(before);
        user.amount = user.amount.add(_amount);

        // Update total boosted share.
        pool.totalBoostedShare = pool.totalBoostedShare.add(_amount.mul(multiplier).div(BOOST_PRECISION));
    }

    user.rewardDebt = user.amount.mul(multiplier).div(BOOST_PRECISION).mul(pool.accCakePerShare).div(
        ACC_CAKE_PRECISION
    );
    poolInfo[_pid] = pool;

    emit Deposit(msg.sender, _pid, _amount);
}
```

### withdraw

```
// Withdraw LP tokens from pool.
/// @param _pid The id of the pool. See `poolInfo`.
/// @param _amount Amount of LP tokens to withdraw.
function withdraw(uint256 _pid, uint256 _amount) external nonReentrant {
    PoolInfo memory pool = updatePool(_pid);
    UserInfo storage user = userInfo[_pid][msg.sender];

    require(user.amount >= _amount, "withdraw: Insufficient");

    uint256 multiplier = getBoostMultiplier(msg.sender, _pid);

    settlePendingCake(msg.sender, _pid, multiplier);

    if (_amount > 0) {
        user.amount = user.amount.sub(_amount);
        lpToken[_pid].safeTransfer(msg.sender, _amount);
    }

    user.rewardDebt = user.amount.mul(multiplier).div(BOOST_PRECISION).mul(pool.accCakePerShare).div(
        ACC_CAKE_PRECISION
    );
    poolInfo[_pid].totalBoostedShare = poolInfo[_pid].totalBoostedShare.sub(
        _amount.mul(multiplier).div(BOOST_PRECISION)
    );

    emit Withdraw(msg.sender, _pid, _amount);
}
```

## ËæÖÂä©ÂáΩÊï∞

- `onlyBoostContract`
- `poolLength()`
- `pendingCake(_pid, _user)`
- `massUpdatePools()`
- `cakePerBlock(_isRegular)`
- `cakePerBlockToBurn()`
- `harvestFromMasterChef()`
- `getBoostMultiplier(_user, _pid)`

### onlyBoostContract Âú∞ÂùÄÈôêÂà∂

```
modifier onlyBoostContract() {
    require(boostContract == msg.sender, "Ownable: caller is not the boost contract");
    _;
}
```

‰ΩøÁî®ÂáΩÊï∞‰øÆÊîπÂô® `onlyBoostContract` ÈôêÂà∂‰∏Ä‰∫õÂè™ËÉΩ `boostContract` Ë∫´‰ªΩÁöÑË∞ÉÁî®ÔºåÁ±ª‰ººÂæàÂ§öÂêàÁ∫¶‰∏≠ÁöÑ onlyOwner„ÄÇ

### poolLength Ê±†Â≠êÊÄªÊï∞Èáè

```
function poolLength() public view returns (uint256 pools) {
    pools = poolInfo.length;
}
```

### pendingCake Êú™È¢ÜÂèñÂ•ñÂä±

```
function pendingCake(uint256 _pid, address _user) external view returns (uint256) {
    PoolInfo memory pool = poolInfo[_pid];
    UserInfo memory user = userInfo[_pid][_user];
    uint256 accCakePerShare = pool.accCakePerShare;
    uint256 lpSupply = pool.totalBoostedShare;

    if (block.number > pool.lastRewardBlock && lpSupply != 0) {
        uint256 multiplier = block.number.sub(pool.lastRewardBlock);

        uint256 cakeReward = multiplier.mul(cakePerBlock(pool.isRegular)).mul(pool.allocPoint).div(
            (pool.isRegular ? totalRegularAllocPoint : totalSpecialAllocPoint)
        );
        accCakePerShare = accCakePerShare.add(cakeReward.mul(ACC_CAKE_PRECISION).div(lpSupply));
    }

    uint256 boostedAmount = user.amount.mul(getBoostMultiplier(_user, _pid)).div(BOOST_PRECISION);
    return boostedAmount.mul(accCakePerShare).div(ACC_CAKE_PRECISION).sub(user.rewardDebt);
}
```

### massUpdatePools Êõ¥Êñ∞ÊâÄÊúâÊ±†Â≠êÁ≥ªÊï∞

```
function massUpdatePools() public {
    uint256 length = poolInfo.length;
    for (uint256 pid = 0; pid < length; ++pid) {
        PoolInfo memory pool = poolInfo[pid];
        if (pool.allocPoint != 0) {
            updatePool(pid);
        }
    }
}
```

### cakePerBlock ËÆ°ÁÆóÊØè‰∏™ÂùóÁöÑ‰∫ßÂá∫Êï∞Èáè

```
function cakePerBlock(bool _isRegular) public view returns (uint256 amount) {
    if (_isRegular) {
        amount = MASTERCHEF_CAKE_PER_BLOCK.mul(cakeRateToRegularFarm).div(CAKE_RATE_TOTAL_PRECISION);
    } else {
        amount = MASTERCHEF_CAKE_PER_BLOCK.mul(cakeRateToSpecialFarm).div(CAKE_RATE_TOTAL_PRECISION);
    }
}
```

### cakePerBlockToBurn ËÆ°ÁÆóÊØè‰∏™ÂùóÁöÑÈîÄÊØÅÊï∞Èáè

```
function cakePerBlockToBurn() public view returns (uint256 amount) {
    amount = MASTERCHEF_CAKE_PER_BLOCK.mul(cakeRateToBurn).div(CAKE_RATE_TOTAL_PRECISION);
}
```

### harvestFromMasterChef ‰æõ V1 ÂçáÁ∫ß V2

```
function harvestFromMasterChef() public {
    MASTER_CHEF.deposit(MASTER_PID, 0);
}
```

‰ªé V1 ÁâàÊú¨Ëé∑Âèñ CAKEÔºåÂπ∂‰∏îÂ∞Ü `PID` ÂÜôÂÖ•Âà∞ V2 ÁâàÊú¨

### getBoostMultiplier Ëé∑ÂèñÁî®Êà∑ÁöÑÂä†ÊùÉ‰πòÊï∞

```
function getBoostMultiplier(address _user, uint256 _pid) public view returns (uint256) {
    uint256 multiplier = userInfo[_pid][_user].boostMultiplier;
    return multiplier > BOOST_PRECISION ? multiplier : BOOST_PRECISION;
}
```

## Êõ¥Êñ∞Êï∞ÊçÆ

- `updateBurnAdmin(_newAdmin)`
- `updateWhiteList(_user, _isValid)`
- `updateBoostContract(address _newBoostContract)`
- `updateBoostMultiplier(_user,_pid,_newMultiplier)`

### updateBurnAdmin Êõ¥Êñ∞ burnAdmin

üîí onlyOwner Ë∞ÉÁî®

```
function updateBurnAdmin(address _newAdmin) external onlyOwner {
    require(_newAdmin != address(0), "MasterChefV2: Burn admin address must be valid");
    require(_newAdmin != burnAdmin, "MasterChefV2: Burn admin address is the same with current address");
    address _oldAdmin = burnAdmin;
    burnAdmin = _newAdmin;
    emit UpdateBurnAdmin(_oldAdmin, _newAdmin);
}

```

### updateWhiteList Êõ¥Êñ∞ÁôΩÂêçÂçï

```
function updateWhiteList(address _user, bool _isValid) external onlyOwner {
    require(_user != address(0), "MasterChefV2: The white list address must be valid");

    whiteList[_user] = _isValid;
    emit UpdateWhiteList(_user, _isValid);
}
```

### updateBoostContract Êõ¥Êñ∞ boostContract

```
function updateBoostContract(address _newBoostContract) external onlyOwner {
    require(
        _newBoostContract != address(0) && _newBoostContract != boostContract,
        "MasterChefV2: New boost contract address must be valid"
    );

    boostContract = _newBoostContract;
    emit UpdateBoostContract(_newBoostContract);
}
```

### updateBoostMultiplier Êõ¥Êñ∞Áî®Êà∑ÁöÑÂä†ÊùÉÁ≥ªÊï∞

```
function updateBoostMultiplier(
    address _user,
    uint256 _pid,
    uint256 _newMultiplier
) external onlyBoostContract nonReentrant {
    require(_user != address(0), "MasterChefV2: The user address must be valid");
    require(poolInfo[_pid].isRegular, "MasterChefV2: Only regular farm could be boosted");
    require(
        _newMultiplier >= BOOST_PRECISION && _newMultiplier <= MAX_BOOST_PRECISION,
        "MasterChefV2: Invalid new boost multiplier"
    );

    PoolInfo memory pool = updatePool(_pid);
    UserInfo storage user = userInfo[_pid][_user];

    uint256 prevMultiplier = getBoostMultiplier(_user, _pid);
    settlePendingCake(_user, _pid, prevMultiplier);

    user.rewardDebt = user.amount.mul(_newMultiplier).div(BOOST_PRECISION).mul(pool.accCakePerShare).div(
        ACC_CAKE_PRECISION
    );
    pool.totalBoostedShare = pool.totalBoostedShare.sub(user.amount.mul(prevMultiplier).div(BOOST_PRECISION)).add(
        user.amount.mul(_newMultiplier).div(BOOST_PRECISION)
    );
    poolInfo[_pid] = pool;
    userInfo[_pid][_user].boostMultiplier = _newMultiplier;

    emit UpdateBoostMultiplier(_user, _pid, prevMultiplier, _newMultiplier);
}
```

## ÈîÄÊØÅ CAKE

ËÆ°ÁÆóÊâÄÊúâÈúÄË¶ÅÈîÄÊØÅÁöÑ CAKEÔºåÂπ∂ËΩ¨Áªô `burnAdmin` Âú∞ÂùÄ„ÄÇ

```
function burnCake(bool _withUpdate) public onlyOwner {
    if (_withUpdate) {
        massUpdatePools();
    }

    uint256 multiplier = block.number.sub(lastBurnedBlock);
    uint256 pendingCakeToBurn = multiplier.mul(cakePerBlockToBurn());

    // SafeTransfer CAKE
    _safeTransfer(burnAdmin, pendingCakeToBurn);
    lastBurnedBlock = block.number;
}
```

## Êõ¥Êñ∞ CAKE ‰∫ßÂá∫Á≥ªÊï∞

Êõ¥Êñ∞ÈîÄÊØÅÈÉ®ÂàÜÔºåÂ∏∏ËßÑÊ±†ÔºåÁâπÊÆäÊ±†ÁöÑ CAKE ÂàÜÈÖçÁôæÂàÜÊØî„ÄÇ

```
// _burnRate        ÊØè‰∏™ÂùóÁöÑÈîÄÊØÅÊØî‰æã
// _regularFarmRate ÊØè‰∏™ÂùóÁöÑÊôÆÈÄöÊ±†ÂàÜÈÖçÊØî‰æã
// _specialFarmRate ÊØè‰∏™ÂùóÁöÑÁâπÊÆäÊ±†ÂàÜÈÖçÊØî‰æã
// _withUpdate      ÊòØÂê¶Ë∞ÉÁî®"massUpdatePools"Êõ¥Êñ∞ÊâÄÊúâÊ±†Êìç‰Ωú„ÄÇ
function updateCakeRate(
    uint256 _burnRate,
    uint256 _regularFarmRate,
    uint256 _specialFarmRate,
    bool _withUpdate
) external onlyOwner {
    require(
        _burnRate > 0 && _regularFarmRate > 0 && _specialFarmRate > 0,
        "MasterChefV2: Cake rate must be greater than 0"
    );
    require(
        _burnRate.add(_regularFarmRate).add(_specialFarmRate) == CAKE_RATE_TOTAL_PRECISION,
        "MasterChefV2: Total rate must be 1e12"
    );
    if (_withUpdate) {
        massUpdatePools();
    }
    // burn cake base on old burn cake rate
    burnCake(false);

    cakeRateToBurn = _burnRate;
    cakeRateToRegularFarm = _regularFarmRate;
    cakeRateToSpecialFarm = _specialFarmRate;

    emit UpdateCakeRate(_burnRate, _regularFarmRate, _specialFarmRate);
}
```

## ÈÄÉÁîüÈÄöÈÅì

‰ΩøÁî® `emergencyWithdraw(_pid)` ÂèØ‰ª•ËøõË°åËµÑÈáëÈÄÉÁîüÔºåËØ•ÊñπÊ≥ïÊèêÂèñÊâÄÊúâÊú¨ÈáëÔºåÂøΩÁï•Â•ñÂä±ÔºåÂπ∂ÊääÁî®Êà∑ÁöÑÊâÄÊúâÊï∞ÊçÆÂàùÂßãÂåñ„ÄÇ

```
function emergencyWithdraw(uint256 _pid) external nonReentrant {
    PoolInfo storage pool = poolInfo[_pid];
    UserInfo storage user = userInfo[_pid][msg.sender];

    uint256 amount = user.amount;
    user.amount = 0;
    user.rewardDebt = 0;
    uint256 boostedAmount = amount.mul(getBoostMultiplier(msg.sender, _pid)).div(BOOST_PRECISION);
    pool.totalBoostedShare = pool.totalBoostedShare > boostedAmount ? pool.totalBoostedShare.sub(boostedAmount) : 0;

    // Note: transfer can fail or succeed if `amount` is zero.
    lpToken[_pid].safeTransfer(msg.sender, amount);
    emit EmergencyWithdraw(msg.sender, _pid, amount);
}
```

## ÂÆâÂÖ®ËΩ¨Ë¥¶

```
function _safeTransfer(address _to, uint256 _amount) internal {
    if (_amount > 0) {
        // Check whether MCV2 has enough CAKE. If not, harvest from MCV1.
        if (CAKE.balanceOf(address(this)) < _amount) {
            harvestFromMasterChef();
        }
        uint256 balance = CAKE.balanceOf(address(this));
        if (balance < _amount) {
            _amount = balance;
        }
        CAKE.safeTransfer(_to, _amount);
    }
}
```
